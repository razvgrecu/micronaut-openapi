plugins {
    id("com.github.johnrengelman.shadow") version "8.1.1"
    id("io.micronaut.application") version "4.0.2"
    id("io.micronaut.aot") version "4.0.2"
    id "org.openapi.generator" version "7.0.0-beta"
}

version = "0.1"
group = "com.example"

repositories {
    mavenCentral()
}

dependencies {
    annotationProcessor("io.micronaut.openapi:micronaut-openapi")
    annotationProcessor("io.micronaut.serde:micronaut-serde-processor")
    implementation("io.micronaut.serde:micronaut-serde-jackson")
    implementation("io.swagger.core.v3:swagger-annotations")
    runtimeOnly("ch.qos.logback:logback-classic")
    testImplementation("io.micronaut:micronaut-http-client")

    implementation(
            project(":api:test1"),
            project(":api:test2"),
            project(":api:test3"),
    )
}


application {
    mainClass.set("com.example.Application")
}
java {
    sourceCompatibility = JavaVersion.toVersion("17")
    targetCompatibility = JavaVersion.toVersion("17")
}

tasks.withType(JavaCompile).configureEach {
    options.fork = true
    outputs.cacheIf {false }
    outputs.upToDateWhen { false }
    options.forkOptions.jvmArgs << '-Dmicronaut.openapi.property.naming.strategy=KEBAB_CASE'
    options.forkOptions.jvmArgs << "-Dmicronaut.openapi.additional.files=${rootProject.buildDir}/openapi-parts".toString()
    options.forkOptions.jvmArgs << "-Dmicronaut.openapi.target.file=${rootProject.buildDir}/specs/openapi.yaml".toString()
}

graalvmNative.toolchainDetection = false
micronaut {
    runtime("netty")
    testRuntime("junit5")
    processing {
        incremental(true)
        annotations("com.example.*")
    }
    aot {
        // Please review carefully the optimizations enabled below
        // Check https://micronaut-projects.github.io/micronaut-aot/latest/guide/ for more details
        optimizeServiceLoading = false
        convertYamlToJava = false
        precomputeOperations = true
        cacheEnvironment = true
        optimizeClassLoading = true
        deduceEnvironment = true
        optimizeNetty = true
    }
}

openApiGenerate {
    generatorName = "typescript-axios"
    inputSpec = "${rootProject.buildDir}/specs/openapi.yaml"
    outputDir = "$buildDir/generated/openapi-axios"
    apiPackage = "org.openapi.example.api"
    invokerPackage = "org.openapi.example.invoker"
    modelPackage = "org.openapi.example.model"
}

